plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace 'org.psilynx.psikit.rr'
    compileSdk 35

    defaultConfig {
        minSdk 24

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = "org.psilynx.psikit"
                artifactId = "rr"
                version = "0.1.0-beta1"

                from components.release

            }
        }

        repositories {
            def dairyReleaseUsername = providers.gradleProperty("dairyReleaseUsername").orNull
            def dairyReleasePassword = providers.gradleProperty("dairyReleasePassword").orNull
            if (dairyReleaseUsername != null && dairyReleasePassword != null) {
                maven {
                    name = "dairyRelease"
                    url = uri("https://repo.dairy.foundation/releases")

                    credentials {
                        username = dairyReleaseUsername
                        password = dairyReleasePassword
                    }

                    authentication {
                        basic(BasicAuthentication)
                    }
                }
            }
        }
    }
}


repositories {
    google()
    mavenCentral()
    def dairyPrivateUsername = providers.gradleProperty("dairyPrivateUsername").orNull
    def dairyPrivatePassword = providers.gradleProperty("dairyPrivatePassword").orNull
    if (dairyPrivateUsername != null && dairyPrivatePassword != null) {
        maven {
            name = "dairyPrivate"
            url = uri("https://repo.dairy.foundation/private")

            credentials {
                username = dairyPrivateUsername
                password = dairyPrivatePassword
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    maven { url "https://repo.dairy.foundation/releases" }
    maven { url "https://maven.brott.dev" }
}

dependencies {
    implementation "com.acmerobotics.roadrunner:ftc:0.1.25"

    api "org.firstinspires.ftc:RobotCore:10.3.0"
    api "org.firstinspires.ftc:Hardware:10.3.0"

    implementation project(":core")

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "org.jetbrains.kotlin:kotlin-reflect:2.0.0"
    implementation project(':core')

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation "org.robolectric:robolectric:4.12.1"
    testImplementation 'junit:junit:4.12'
    testImplementation "io.mockk:mockk:1.14.5"
    testImplementation "io.mockk:mockk-android:1.14.5"
    testImplementation "io.mockk:mockk-agent:1.14.5"

    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
